'' =================================================================================================
''
''   File....... isp_hub75_demoColor.spin2
''   Purpose.... Top-level Demonstration - showing off our HUB75 matrix
''   Authors.... Stephen M Moraco
''               -- Copyright (c) 2020 Iron Sheep Productions, LLC
''               -- see below for terms of use
''   E-mail..... stephen@ironsheep.biz
''   Started.... Oct 2020
''   Updated.... 17 Oct 2020
''
'' =================================================================================================


CON { timing }

  CLK_FREQ = 335_000_000                                        ' system freq as a constant
  _clkfreq = CLK_FREQ                                           ' set system clock

CON { DEBUG PINs }

'DEBUG_PIN = 16

DEMO_COUNT = 11

VAR

    long    cog
    long    demoPausePeriod

OBJ

    color       : "isp_hub75_color"
    pixels      : "isp_hub75_screenUtils"
    screen      : "isp_hub75_screenAccess"
    display     : "isp_hub75_display"

PUB main() : ok | demoIndex
    '' start underlying drivers and run demo

    'repeat

    'debug("`term temp size 82 50 textsize 10")

    ok := cog := display.start()   ' send buffer to driver
    if ok == -1
        debug("- DEMO: underlying drivers failed!")
        abort
{
    demoIndex := 0
    'repeat
        repeat demoIndex from 0 to 5
            display.clearScreen()
            case demoIndex
                0: demoTest256ColorBrightness()
                1: demoTest32ColorBrightness()
                2: demoTest16ColorBrightness()
                3: demoTest360ColorBrightness()
                4: demoTestGammaTuneBrightness()
                5: demoTest12x9ColorBrightness()
            waitSec(5)
    repeat
'}
{
    demoTest12x9ColorBrightness()
    repeat
'}
    'demoColorPalette12Panels()
    'demoScreenBlastRed()
    'repeat
    'demoScreenBlastWhite()
    'repeat
{
    demoAlignmentText()
    repeat
'}
    demoPausePeriod := 3   ' seconds
    repeat 20
        repeat demoIndex from 0 to DEMO_COUNT - 1
            display.clearScreen()
            case demoIndex + 1
                1: demoText()
                2: demoParallax()
                3: demoColorBrightness()
                4: demoHueGroups()  ' needs fixing
                5: demoHueLines()
                6: demoColorPalette12Panels()
                7: showFullColorSweep()
                8: demoTest12x9ColorBrightness()
                9: colorBlast()
               10: demoText()
               11: demoScreenBlast()

            if demoIndex + 1 <> 11
                waitSec(demoPausePeriod)

    stop()

PRI stop()
    '' stop the driver
    if cog
        display.stop()
        cog := 0

PRI waitSec(countSeconds)
    repeat countSeconds
        waitms(1000)

    ' do DEMO Things!

PUB demoParallax() | rtCol, ltCol, topRow, botRow
    display.setTextFont(display.TEXT_FONT_5x7)    ' font size 
    display.setCursor(0, 0)   ' start at top left, with one pixel of spacing

    ltCol := 0
    rtCol := screen.MAX_PHYSICAL_COLUMNS - 1
    topRow := 0
    botRow := screen.MAX_PHYSICAL_ROWS - 1

    display.setColoredTextAtLnCol(1, 0, string(" Parallax"), color.cBlue)
    display.drawLineOfColor(botRow, ltCol, botRow, rtCol, color.cGreen)     ' bottom Line
    display.drawLineOfColor(topRow, ltCol, botRow, ltCol, color.cRed)      ' left line
    display.drawLineOfColor(topRow, ltCol, topRow, rtCol, color.cYellow)    ' top line
    display.drawLineOfColor(topRow, rtCol, botRow, rtCol, color.cMagenta)   ' right line
    display.setColoredTextAtLnCol(2, 0, string("Propeller2"), color.cBlue)
    display.commitScreenToPanel()

PUB demoAlignmentText()
    display.setTextFont(display.TEXT_FONT_5x7)    ' font size 
    display.setCursor(0, 0)   ' start at top left, with one pixel of spacing
    display.setColoredTextAtLnCol(0, 0, string("Line 1"), color.cBlue)
    display.setColoredTextAtLnCol(1, 0, string("Propeller"), color.cBlue)
    display.setColoredTextAtLnCol(2, 0, string("Propeller2"), color.cBlue)
    display.setColoredTextAtLnCol(3, 0, string("Line 4"), color.cBlue)
    display.commitScreenToPanel()

PUB demoText()
    '' draw some text!
    display.setTextFont(display.TEXT_FONT_5x7)    ' font size 
    display.setCursor(0, 0)   ' start at top left, with one pixel of spacing

    display.setColoredTextAtLnCol(0,0, string("iron sheep"), color.cMaroon)
    display.setColoredTextAtLnCol(1,0, string("-RGB 3bit-"), color.cOrange)
    if screen.MAX_PWM_FRAMES == 8
        display.setColoredTextAtLnCol(2,0, string("(8frm pwm)"), color.cRainbow)
    elseif screen.MAX_PWM_FRAMES == 12
        display.setColoredTextAtLnCol(2,0, string("12frm pwm!"), color.cRainbow)
    elseif screen.MAX_PWM_FRAMES == 16
        display.setColoredTextAtLnCol(2,0, string("16frm pwm!"), color.cRainbow)
    elseif screen.MAX_PWM_FRAMES == 20
        display.setColoredTextAtLnCol(2,0, string("20frm pwm!"), color.cRainbow)
    elseif screen.MAX_PWM_FRAMES == 23
        display.setColoredTextAtLnCol(2,0, string("23frm pwm!"), color.cRainbow)
    elseif screen.MAX_PWM_FRAMES == 24
        display.setColoredTextAtLnCol(2,0, string("24frm pwm!"), color.cRainbow)
    elseif screen.MAX_PWM_FRAMES == 32
        display.setColoredTextAtLnCol(2,0, string("32frm pwm!"), color.cRainbow)
    display.setColoredTextAtLnCol(3,0, string("led matrix"), color.cWhite)
    display.commitScreenToPanel()

PUB demoTestGammaTuneBrightness() | red, green, blue, row, col, rowOffset, colorGroup, bUseRed, bUseGreen, bUseBlue, colorValue

    repeat colorGroup from 0 to 2
        bUseRed := bUseGreen := bUseBlue := FALSE
        case colorGroup
            0:
                row := 4
                col := 6
                bUseRed := bUseGreen := TRUE
            1:
                row := 4
                col := 37
                bUseBlue := bUseGreen := TRUE
            2:
                row := 16
                col := 22
                bUseBlue := bUseRed := TRUE
            other:
                debug("- invalid color group index")
                abort

        rowOffset := 0
        repeat colorValue from 15 to 255 step 35
            red := (bUseRed) ? colorValue : 0
            green := (bUseGreen) ? colorValue : 0
            blue := (bUseBlue) ? colorValue : 0
            drawGammaTuneSwatch(7, row + rowOffset, col, red, green, blue)
            rowOffset++

    display.commitScreenToPanel()

PRI drawGammaTuneSwatch(width, row, col, red, green, blue) | ltCol, midCol, rtCol, colOffset, ltClr, midClr, rtClr
    ltCol := col
    midCol := col + width
    rtCol := col + (width * 2)
    '     combinedValue := ((red & $ff) << 16) | ((green & $ff) << 8) | (blue & $ff)
    green := green << 8
    red := red << 16
    if red == 0
        ltClr := green
        midClr := green | blue
        rtClr := blue
    elseif green == 0
        ltClr := blue
        midClr := blue | red
        rtClr := red
    else ' blue == 0
        ltClr := red
        midClr := red | green
        rtClr := green

    repeat colOffset from 0 to width - 1
        pixels.drawPixelAtRC(row, ltCol + colOffset, ltClr)
        pixels.drawPixelAtRC(row, midCol + colOffset, midClr)
        pixels.drawPixelAtRC(row, rtCol + colOffset, rtClr)


PUB demoTest12x9ColorBrightness() | red, green, blue, row, col, rowCtr, colCtr, clrLow, clrMed, clrHigh, clrBW, bThirdClr

    repeat rowCtr from 0 to 8
        clrLow := 0
        case rowCtr
            0:
                clrMed := 25
                clrHigh := 51
                clrBW := 0
            1:
                clrMed := 51
                clrHigh := 102
                clrBW := 32
            2:
                clrMed := 76
                clrHigh := 153
                clrBW := 64
            3:
                clrMed := 102
                clrHigh := 204
                clrBW := 96
            4:
                clrMed := 128
                clrHigh := 255
                clrBW := 128
            5:
                clrMed := 128
                clrHigh := 255
                clrBW := 128
            6:
                clrMed := 51
                clrHigh := 255
                clrBW := 128
                clrLow := 51
            7:
                clrMed := 102
                clrHigh := 255
                clrBW := 128
                clrLow := 102
            8:
                clrMed := 153
                clrHigh := 255
                clrBW := 128
                clrLow := 153
            9:
                clrMed := 204
                clrHigh := 255
                clrBW := 128
                clrLow := 204
            other:
                clrMed := 0
                clrHigh := 0
                clrBW := 0

        clrHigh := color.gammaPeek(clrHigh)
        clrMed := color.gammaPeek(clrMed)
        clrLow := color.gammaPeek(clrLow)
        clrBW := color.gammaPeek(clrBW)

        repeat colCtr from 0 to 12  ' last is BW
            red := green := blue := 0
            case colCtr
                0:  ' rH
                    red := clrHigh
                    green := blue := clrLow
                1:  ' rH, gM
                    red := clrHigh
                    green := clrMed
                    blue := clrLow
                2:  ' rH, gH
                    red := clrHigh
                    green := clrHigh
                    blue := clrLow
                3:  ' rM, gH
                    red := clrMed
                    green := clrHigh
                    blue := clrLow
                4:  ' gH
                    green := clrHigh
                    red := blue := clrLow
                5:  ' gH, bM
                    green := clrHigh
                    blue := clrMed
                    red := clrLow
                6:  ' gH, bH
                    green := clrHigh
                    blue := clrHigh
                    red := clrLow
                7:  ' gM, bH
                    green := clrMed
                    blue := clrHigh
                    red := clrLow
                8:  ' bH
                    blue := clrHigh
                    red := green := clrLow
                9:  ' rM, bH
                    red := clrMed
                    blue := clrHigh
                    green := clrLow
                10:  ' rH, bH
                    red := clrHigh
                    blue := clrHigh
                    green := clrLow
                11:  ' rH, bM
                    red := clrHigh
                    blue := clrMed
                    green := clrLow
                12:
                    red := blue := green := clrBW

            row := rowCtr * 3
            if rowCtr <> 0 && rowCtr // 2 == 0
                row++
            col := colCtr * 5
            drawSwatch(4, 3, row, col, color.cValueForRGB(red, green, blue))
    display.commitScreenToPanel()

PRI drawSwatch(width, height, row, col, rgbColor) | rowOffset, colOffset
    repeat rowOffset from 0 to height - 1
        repeat colOffset from 0 to width - 1
            pixels.drawPixelAtRC(row + rowOffset, col + colOffset, rgbColor)

PRI demoTest360ColorBrightness() | red, green, blue, row, col, rowCtr, degrees, colorValue

    row := col := 0
    repeat degrees from 0 to 359
        colorValue := color.rgbColorFromDegrees(degrees)
        repeat rowCtr from 0 to 4
            pixels.drawPixelAtRC(row, col, colorValue)
            col++
            if col > 63
                col := 0
                row++
    display.commitScreenToPanel()


PRI demoTest16ColorBrightness() | red, green, blue, row, col, rowCtr, colorIndex, useRed, useBlue, useGreen, colorValue, colorHue
    row := 0

    repeat colorHue from 0 to 255 step 16
        colorValue := color.rgbColorFromDegrees(colorHue)
        repeat rowCtr from 0 to 2
            repeat col from 0 to 63
                pixels.drawPixelAtRC(row, col, colorValue)
            row++
    display.commitScreenToPanel()


PRI demoTest32ColorBrightness() | red, green, blue, row, col, rowCtr, colorIndex, useRed, useBlue, useGreen, colorValue

    row := 0

    repeat colorIndex from 0 to 6
        useRed := useGreen := useBlue := FALSE
        case colorIndex
            0: ' red
                useRed := TRUE
            1: ' green
                useGreen := TRUE
            2: ' blue
                useBlue := TRUE
            3: ' cyan
                useRed := useBlue := TRUE
            4: ' agenta
                useRed := useGreen  := TRUE
            5: ' yellow
                useGreen := useBlue := TRUE
            OTHER: ' white
                useRed := useGreen := useBlue := TRUE

        colorValue := 0
        repeat rowCtr from 0 to 3
            repeat col from 0 to 63
                red := green := blue := 0
                if useRed
                    red := colorValue
                if useGreen
                    green := colorValue
                if useBlue
                    blue := colorValue

                pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
                if col // 4 == 0
                    colorValue++
            row++

    display.commitScreenToPanel()

PRI demoTest256ColorBrightness() | red, green, blue, row, col, rowCtr, colorIndex, useRed, useBlue, useGreen, colorValue

    row := 0

    repeat colorIndex from 0 to 7
        useRed := useGreen := useBlue := FALSE
        case colorIndex
            0: ' red
                useRed := TRUE
            1: ' green
                useGreen := TRUE
            2: ' blue
                useBlue := TRUE
            3: ' cyan
                useRed := useBlue := TRUE
            4: ' agenta
                useRed := useGreen  := TRUE
            5: ' yellow
                useGreen := useBlue := TRUE
            OTHER: ' white
                useRed := useGreen := useBlue := TRUE

        colorValue := 0
        repeat rowCtr from 0 to 3
            repeat col from 0 to 63
                red := green := blue := 0
                if useRed
                    red := colorValue
                if useGreen
                    green := colorValue
                if useBlue
                    blue := colorValue

                pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
                colorValue++
            row++

    display.commitScreenToPanel()

    'repeat  ' hold here!
PRI demoColorBrightness() | red, green, blue, row, col, rowCtr, colorIndex, useRed, useBlue, useGreen

    row := 0

    repeat colorIndex from 0 to 7
        useRed := useGreen := useBlue := FALSE
        case colorIndex
            0: ' red
                useRed := TRUE
            1: ' green
                useGreen := TRUE
            2: ' blue
                useBlue := TRUE
            3: ' cyan
                useRed := useBlue := TRUE
            4: ' agenta
                useRed := useGreen  := TRUE
            5: ' yellow
                useGreen := useBlue := TRUE
            OTHER: ' white
                useRed := useGreen := useBlue := TRUE

        repeat rowCtr from 0 to 3
            repeat col from 0 to 63
                red := green := blue := 0
                if useRed
                    red := col * 4
                if useGreen
                    green := col * 4
                if useBlue
                    blue := col * 4
                pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
            row++

    display.commitScreenToPanel()

    'repeat  ' hold here!

    
PRI demoHueLines() | row, col, colorOffset
    '' COLOR PANEL using hue  - all in one line 
    col := 2
    repeat row from 0 to 31
        repeat colorOffset from 0 to 350 step 6
            pixels.drawPixelAtRC(row, col, color.rgbColorFromDegrees(colorOffset))
            col++
            if col > 62         ' 62 is fun!  60 is desired
                col := 2
    display.commitScreenToPanel()

PRI demoHueLinesToo() | row, col, colorOffset
    '' COLOR PANEL using hue  - all in one line 
    col := 0
    repeat row from 0 to 31
        repeat colorOffset from 0 to 350 step 6
            pixels.drawPixelAtRC(row, col, color.rgbColorFromDegrees(colorOffset))
            col++
            if col > 63         ' 62 is fun!  60 is desired
                col := 0
    display.commitScreenToPanel()


PUB demoHueGroups() | row, col, green, panelNumber, colorOffset
    '' COLOR PANEL using hue  6 groups
    row := 0
    repeat panelNumber from 0 to 350 step 60
        repeat colorOffset from 0 to 4
            repeat col from 0 to 59
                    green := panelNumber + col
                    pixels.drawPixelAtRC(row, col, color.rgbColorFromDegrees(green))
            row++
    display.commitScreenToPanel()

PUB demoColorPalette12Panels() | red, panelNumber
    '' 10x10x10 COLOR PANELs + BW panel
    panelNumber := 0
    repeat red from 0 to 255 step 25
        generateGreenBluePanelAgainst(panelNumber, red)
        panelNumber++
    generateBWPanel(11)
    display.commitScreenToPanel()

PRI generateGreenBluePanelAgainst(panelNumber, red) | panelRow, panelCol, row, col, green, blue, startCol
    panelRow := panelNumber / 4
    panelCol := panelNumber // 4
    row := panelRow * 11
    startCol := col := panelCol * 17
    'debug("clr panelNbr=(", udec_(panelNumber), "),startRC=(", udec_(row), ",", udec_(col), ")")
    repeat green from 0 to 255 step 25
        repeat blue from 0 to 255 step 25
            pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
            col++
        row++
        col := startCol

PRI generateBWPanel(panelNumber) | panelRow, panelCol, row, col, red, green, blue, startCol, colCtr
    panelRow := panelNumber / 4
    panelCol := panelNumber // 4
    row := panelRow * 11
    startCol := col := panelCol * 17
    'debug("bw panelNbr=(", udec_(panelNumber), "),startRC=(", udec_(row), ",", udec_(col), ")")
    colCtr := 0
    repeat green from 0 to 255 step 2
        red := blue := green
        pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
        col++
        colCtr++
        if colCtr >= 11
            colCtr := 0
            row++
            col := startCol
            'debug("bw panelNbr=(", udec_(panelNumber), "),startRC=(", udec_(row), ",", udec_(col), ")")

PUB showFullColorSweep() | row, col, red, green, blue
    '' basic bands - full color sweep
    red := green := blue := 0
    row := 2
    col := 0
    repeat red from 0 to 255        ' RED
        pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
        col++
        if (col > screen.MAX_PANEL_COLUMNS - 1)
            row++
            col := 0
    red := green := blue := 0
    repeat green from 0 to 255      ' GREEN
        pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
        col++
        if (col > screen.MAX_PANEL_COLUMNS - 1)
            row++
            col := 0
    red := green := blue := 0
    repeat blue from 0 to 255       ' BLUE
        pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
        col++
        if (col > screen.MAX_PANEL_COLUMNS - 1)
            row++
            col := 0
    red := green := blue := 0
    repeat red from 0 to 255        ' YELLOW'
        green := red
        pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
        col++
        if (col > screen.MAX_PANEL_COLUMNS - 1)
            row++
            col := 0
    red := green := blue := 0
    repeat green from 0 to 255      ' CYAN
        blue := green
        pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
        col++
        if (col > screen.MAX_PANEL_COLUMNS - 1)
            row++
            col := 0
    red := green := blue := 0
    repeat blue from 0 to 255       ' MAGENTA
        red := blue
        pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
        col++
        if (col > screen.MAX_PANEL_COLUMNS - 1)
            row++
            col := 0
    red := green := blue := 0
    repeat blue from 0 to 255       ' WHITE
        green := red := blue
        pixels.drawPixelAtRC(row, col, color.cValueForRGB(red, green, blue))
        col++
        if (col > screen.MAX_PANEL_COLUMNS - 1)
            row++
            col := 0
    red := green := blue := 0
    display.commitScreenToPanel()


PUB demoScreenBlast() | color3bit
    repeat 5
        repeat color3bit from 0 to 7
            display.fillPanel(color3bit)
            waitms(150)


PUB colorBlast() | row, col, red, green, blue, blockX, blockY, subrow, subcol
    '' color blast
    'debug("- DEMO: draw colored pixels")
    repeat blockX from 0 to 3
        repeat blockY from 0 to 1
            repeat subrow from 0 to 15
                repeat subcol from 0 to 15
                    row := (blockY * 16) + subrow
                    col := (blockX * 16) + subcol
                    red := subrow * (255 / 16)
                    green := subcol * (255 / 16)
                    blue := ((blockY * 4) + blockX) * (255 / 8)
                    pixels.drawPixelAtRCwithRGB(row, col, red, green, blue)

    display.commitScreenToPanel()


PUB demoScreenBlastRed() | color3bit
            color3bit := 1
            display.fillPanel(color3bit)

PUB demoScreenBlastWhite() | color3bit
            color3bit := 7
            display.fillPanel(color3bit)


CON { license }

{{


 -------------------------------------------------------------------------------------------------
  MIT License

  Copyright (c) 2020 Iron Sheep Productions, LLC

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
 =================================================================================================
}}

