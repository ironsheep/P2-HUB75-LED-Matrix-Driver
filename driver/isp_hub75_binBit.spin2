'' =================================================================================================
''
''   File....... isp_hub75_binDigit.spin2
''   Purpose.... manipulate a column of 4-bits representing a single digit
''   Authors.... Stephen M Moraco
''               -- Copyright (c) 2020 Iron Sheep Productions, LLC
''               -- see below for terms of use
''   E-mail..... stephen@ironsheep.biz
''   Started.... Oct 2020
''   Updated.... 24 Oct 2020
''
'' =================================================================================================

CON

    ' various states of fill
    #0, STATE_UNKNOWN, STATE_FILLING_1, STATE_FILLING_2, STATE_FILLING_3, STATE_FILLING_4, STATE_FULL, STATE_EMPTYING_4, STATE_EMPTYING_3, STATE_EMPTYING_2, STATE_EMPTYING_1, STATE_EMPTY

OBJ

    screen      : "isp_hub75_screenAccess"
    pixels      : "isp_hub75_screenUtils"
    color       : "isp_hub75_color"
    flow        : "isp_hub75_binflow"

DAT { initialized data }


VAR { globals }
    long    topRow
    long    leftColumn
    long    bitHeight
    long    bitWidth

    long    desiredEndState
    long    pendingEndState
    long    currentState
    long    nextState

    long    fullRgbColor
    long    emptyRgbColor
    
    byte    isSetup
    byte    currBitValue
    byte    waitingOnInflowComplete
    byte    instanceID
    
PUB null()
    '' This is NOT a top level object


{{
    WORK Herein:
    if bit is turning on:
        start-inflow
        when inflow touches bit
            start bit (filling itself)
    elseif bit it turning off
        start bit (emptying itself)
        start outflow
    wiat for animations to complete
}}


PUB placeBitAtRC(row, column, bitId, fullColor, emptyColor) | bitIndex
    debug("bit:place @RC(", udec_(row), ",", udec_(column), ") ", ubin(bitId), uhex_long(fullColor), uhex_long(emptyColor))
    if NOT isSetup
        initialize()
    topRow := row
    leftColumn := column
    instanceID := bitId         ' jut for info / debug, etc.
    fullRgbColor := fullColor
    emptyRgbColor := emptyColor
    currBitValue := 0
    drawBit(topRow, leftColumn, emptyColor)

    '' init flows for this bit
    flow.setUpInAndOutFlows(row, column, bitWidth, bitHeight)

PRI initialize()
    isSetup := TRUE
    bitHeight := 4
    bitWidth := 5
    pendingEndState := desiredEndState := nextState := currentState := STATE_EMPTY


PUB setValue(bitValue) | newEndState
    '' set a new value for this bit of a digit
    'debug("dig:setValue ", ubin(digit))
    if currBitValue <> bitValue
        ' if bit is turning on
        if bitValue == 1    
            ' start inflow
            newEndState := STATE_FULL
            ' we are deferring bit-fill until flow reached the bit
            waitingOnInflowComplete := TRUE
            ' start the in-flow
            flow.startFlow(flow.TYPE_INFLOW)
            ' record the state the bit will go to when flow reaches bit
            pendingEndState := newEndState
        else
            ' bit is turning off
            newEndState := STATE_EMPTY
            ' start digit emptying
            if desiredEndState <> newEndState
                moveBitToState(newEndState)
            ' and start outflow
            flow.startFlow(flow.TYPE_OUTFLOW)
        ' record that we've done this
        currBitValue := bitValue

PUB isMovementComplete() : status | flowIndex
    '' Return T/F where T means that all movement for this bit is complete
    status := (desiredEndState == currentState) ? TRUE : FALSE
    if status == TRUE ' if not done, then don't check further... if done, then check flows!
        if NOT flow.isMovementComplete()
            status := FALSE

PUB animateStep()  | flowIndex, flowAnimateResult, newEndState
    '' Do one animation step, if one is pending
    if not isMovementComplete()
        if not waitingOnInflowComplete
            updateBitToState(nextState)
        if NOT flow.isMovementComplete()
            flowAnimateResult := flow.animateStep()
            if flowAnimateResult == flow.FLOW_AT_BIT
                waitingOnInflowComplete := FALSE
                ' now let's perform our deferred movement (fill the bit!)
                newEndState := pendingEndState
                pendingEndState := STATE_EMPTY
                moveBitToState(newEndState)

PUB moveBitToState(newEndState) 
    '' Start sequence of segment updates to desired state (on/off)
    'debug("bit:id(bit", udec_(currValue), "), curr-st", udec_(currentState), " moveTo (st", udec_(desiredEndState), ") -> (st", udec_(newEndState), ")")
    ' if we are asking to be in a different state then...
    if desiredEndState <> newEndState
        ' if we are not yet at an end state (still moving) then abort
        if currentState <> STATE_EMPTY && currentState <> STATE_FULL
            debug("SEG: state still in motion, ignoring request")
            abort
        if newEndState <> STATE_EMPTY && newEndState <> STATE_FULL
            debug("SEG: BAD end-state must be EMPTY or FULL!")
            abort        
        if newEndState == STATE_FULL
            ' empty filling up
            nextState := STATE_FILLING_1    ' heading to _4 then FULL
        else
            ' full emptying
            nextState := STATE_EMPTYING_4  ' heading to _1 then EMPTY
        updateBitToState(nextState)
        desiredEndState := newEndState
    else
        debug("  -- already in desiredEndState?")

PRI updateBitToState(newState)
    'debug("bit:id(bit", udec_(currValue), "), moveTo (st", udec_(currentState), ") -> (st", udec_(newState), ")")
    if newState <> STATE_FULL
        ' clear the bit
        fillBit(bitHeight, emptyRgbColor)
    ' now draw bit lines that are showing
    case newState
        STATE_FILLING_1:   ' 1 line filled
                fillBit(bitHeight - 4, fullRgbColor)
                nextState := STATE_FILLING_2
        STATE_FILLING_2:   ' 2 lines filled
                fillBit(bitHeight - 3, fullRgbColor)
                nextState := STATE_FILLING_3
        STATE_FILLING_3:   ' 3 lines filled
                fillBit(bitHeight - 2, fullRgbColor)
                nextState := STATE_FILLING_4
        STATE_FILLING_4:   ' 3 lines filled
                fillBit(bitHeight - 1, fullRgbColor)
                nextState := STATE_FULL
        STATE_FULL:   ' all lines filled
                fillBit(bitHeight, fullRgbColor)
                'debug("bit:FULL")
        STATE_EMPTYING_4:  ' 4 lines empty
                fillBit(bitHeight - 1, fullRgbColor)
                nextState := STATE_EMPTYING_3
        STATE_EMPTYING_3:  ' 3 lines empty
                fillBit(bitHeight - 2, fullRgbColor)
                nextState := STATE_EMPTYING_2
        STATE_EMPTYING_2:  ' 2 lines empty
                fillBit(bitHeight - 3, fullRgbColor)
                nextState := STATE_EMPTYING_1
        STATE_EMPTYING_1:  ' 1 line empty
                fillBit(bitHeight - 4, fullRgbColor)
                nextState := STATE_EMPTY
        STATE_EMPTY:  ' nothing to do since we clear segment first
                'debug("bit:EMPTY")
        OTHER:
            debug("STATE(vrtL) not yet implemented (", udec_(newstate), ")")
    currentState := newState

' FIXME: UNDONE state changes would be faster if we filled or cleared just the lines that changed!

PRI fillBit(level, rgbColor) | rowIndex, rowBottom, rowTop
    if level > 0
        rowBottom := topRow + bitHeight - 1
        rowTop := rowBottom - (level - 1)
        repeat rowIndex from rowBottom to rowTop
            drawLine(rowIndex, leftColumn, rowIndex, leftColumn + bitWidth - 1, rgbColor)

PRI drawBit(row, column, rgbColor) | rowIndex
    'debug("dig:drawBit @RC(", udec_(row), ",", udec_(column), ") ", uhex_long(rgbColor))
    repeat rowIndex from row to bitHeight + row - 1
        drawLine(rowIndex, column, rowIndex, column + bitWidth - 1, rgbColor)

PRI drawLine(fmRow, fmColumn, toRow, toColumn, rgbColor) | row, column
    'debug("seg:drwLn fmRC=(", udec_(fmRow), ",", udec_(fmColumn), "), toRC=(", udec_(toRow), ",", udec_(toColumn), "), RGB=(", uhex_long(rgbColor), ")")
    fmRow :=  0 #> fmRow <# screen.MAX_DISPLAY_ROWS - 1
    fmColumn :=  0 #> fmColumn <# screen.MAX_DISPLAY_COLUMNS - 1
    toRow :=  0 #> toRow <# screen.MAX_DISPLAY_ROWS - 1
    toColumn :=  0 #> toColumn <# screen.MAX_DISPLAY_COLUMNS - 1
    if fmRow == toRow
        ' draw Horizontal Line
        repeat column from fmColumn to toColumn 
            pixels.drawPixelAtRCwithRGB(fmRow, column, color.rgbForCValue(rgbColor))
    elseif fmColumn == toColumn
        ' draw Vertical Line
        repeat row from fmRow to toRow 
            pixels.drawPixelAtRCwithRGB(row, fmColumn, color.rgbForCValue(rgbColor))
    else
        debug("DIAG lines not implemented !")
        abort


CON { license }

{{


 -------------------------------------------------------------------------------------------------
  MIT License

  Copyright (c) 2020 Iron Sheep Productions, LLC

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
 =================================================================================================
 
}}

