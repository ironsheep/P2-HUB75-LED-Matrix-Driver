'' =================================================================================================
''
''   File....... isp_hub75_binClock.spin2
''   Purpose.... manipulate a binary clocktouch 
''   Authors.... Stephen M Moraco
''               -- Copyright (c) 2020 Iron Sheep Productions, LLC
''               -- see below for terms of use
''   E-mail..... stephen@ironsheep.biz
''   Started.... Oct 2020
''   Updated.... 24 Oct 2020
''
'' =================================================================================================

' this is a 6 digit binary clock showing HH:MM:SS but in vertical bits
'  animation basic: bits are 5x4 and animate filling up from the bottom and empty from the top
'
CON { public interface constants }

    MAX_DIGITS = 6

    HORIZ_ODD_OFFSET = 5
    HORIZ_EVEN_OFFSET = 7
    DIGIT_WIDTH = 5
    TOP_INSET = 5
    LEFT_INSET = 2

OBJ { objects }

    screen                  : "isp_hub75_screenAccess"
    display                 : "isp_hub75_display"
    color                   : "isp_hub75_color"
    digits[MAX_DIGITS]      : "isp_hub75_binDigit"

DAT { initialized data }

isSetup         BYTE    FALSE
digitPeriod     BYTE    5    ' 12 
segmentPeriod   BYTE    0  ' was 5


VAR { globals }

    long    emptyRgbColor
    long    fullRgbColor
    long    cog
    
    byte    currHours
    byte    currMinutes
    byte    currSeconds

PUB null()
    '' This is NOT a top level object

PUB setOnOffColors(onColor, offColor)
    '' Configure: colors for full clock display
    emptyRgbColor := offColor
    fullRgbColor := onColor  

PUB setupClock() : ok | digitIndex, row, col 
    '' Set up:  draw our initial clock bits
    ok := cog := display.start()   ' send buffer to driver
    if ok == -1
        debug("- DEMO: underlying drivers failed!")
        abort

    row := TOP_INSET
    col := LEFT_INSET
    repeat digitIndex from 0 to MAX_DIGITS - 1
        digits[digitIndex].placeDigitAtRC(row, col, (MAX_DIGITS - 1) - digitIndex + 1, fullRgbColor, emptyRgbColor)
        if (digitIndex & 1) == 0
            ' placed odd bit
            col += HORIZ_ODD_OFFSET + DIGIT_WIDTH
        else
            col += HORIZ_EVEN_OFFSET + DIGIT_WIDTH
    display.commitScreenToPanel()

  '  repeat ' hold here

PUB stopClock() : ok
    '' stop our back-end display driver
  if cog
    display.stop()
    cog := 0

PUB setTimeHMS(hours, minutes, seconds)
    '' configure the clock with the initial time value
    debug("clk:setTimeHMS ", udec(hours), udec(minutes), udec(seconds))

    if isAnimationComplete()
        ' evaluate what needs to change, cause change
                ' TEST checking out flows - tweaking them

        setHMS(hours, minutes, seconds)
        display.commitScreenToPanel()
        wait100thSecs(segmentPeriod)
        loopUntilBitsFinish()

        ' preserve new time value
        currHours := hours
        currMinutes := minutes
        currSeconds := seconds
    else
        debug("ERROR: animation NOT complete, IGNORING setTimeHMS()")

    'debug("--  hold --")
    'repeat


PUB runClock()
    '' simply increment seconds(,minutes,hours) every second (assumes time is already set! - setTimeHMS() was already called

PRI setHMS(hours, minutes, seconds)
    ' set the initial time to be displayed
    'debug("clk:set ", udec(hours), udec(minutes), udec(seconds))
    currHours := hours
    currMinutes := minutes
    currSeconds := seconds

    'return  ' TESTING exit

    digits[0].setValue(hours / 10)
    digits[1].setValue(hours // 10)
    digits[2].setValue(minutes / 10)
    digits[3].setValue(minutes // 10)
    digits[4].setValue(seconds / 10)
    digits[5].setValue(seconds // 10)

    showSecondsDots( seconds // 2 )

PRI isAnimationComplete() : status | digitIndex
    ' Return T/F where T means all segment animations are complete
    status := TRUE
    repeat digitIndex from 0 to MAX_DIGITS - 1
        if not digits[digitIndex].isMovementComplete()
            status := FALSE
            quit   ' we have our answer, abort loop

PRI loopUntilBitsFinish() | digitIndex, didStep
    debug("clk:loopUntilBitsFinish()")
    repeat 
        if not isAnimationComplete() 
            didStep := FALSE
            repeat digitIndex from 0 to MAX_DIGITS - 1
                if not digits[digitIndex].isMovementComplete()
                    digits[digitIndex].animateStep()
                    didStep := TRUE ' at least 1 digit stepped

                if didStep
                    display.commitScreenToPanel()
                    wait100thSecs(segmentPeriod)

        if isAnimationComplete() 
            quit

{

PRI setFlowHMS(hours, minutes, seconds)
    '' set the initial time to be displayed
    'debug("clk:set ", udec(hours), udec(minutes), udec(seconds))

    ' TEST FLOWS FOR NOW...
    feeds.setHMS(hours, minutes, seconds)
    showSecondsDots( ((seconds // 4) // 2) )

PRI isAnimationComplete() : status | digitIndex
    ' Return T/F where T means all segment animations are complete
    status := TRUE
    repeat digitIndex from 0 to MAX_DIGITS - 1
        if not digits[digitIndex].isMovementComplete()
            status := FALSE
            quit   ' we have our answer, abort loop

    if status == TRUE
        status := feeds.isAnimationComplete()

PRI animateStep() | digitIndex
    '' Have each sement do 1 animation step, if one is pending
    if not isAnimationComplete()
        feeds.animateStep()

        repeat digitIndex from 0 to MAX_DIGITS - 1
            if not digits[digitIndex].isMovementComplete()
                digits[digitIndex].animateStep()

}


PRI showSecondsDots(isBright) | rgbColor
    rgbColor := (isBright) ? $ff40ff : $471247
    showDot(12, 19, rgbColor)
    showDot(18, 19, rgbColor)
    showDot(12, 41, rgbColor)
    showDot(18, 41, rgbColor)

PRI showDot(row, column, rgbColor) | rowIndex, columnIndex
    repeat rowIndex from row to row + 2
        repeat columnIndex from column to column + 2   
            drawPixelAtRCwithRGB(rowIndex, columnIndex, color.rgbForCValue(rgbColor))


PRI drawPixelAtRCwithRGB(row, column, red, green, blue) | rowIndex, columnIndex, colorOffset, pColor
    '' Place r,g,b colored pixel at r,c
    rowIndex := 0 #> row <# screen.MAX_DISPLAY_ROWS - 1
    columnIndex :=  0 #> column <# screen.MAX_DISPLAY_COLUMNS - 1
    colorOffset := ((rowIndex * screen.MAX_DISPLAY_COLUMNS) + columnIndex) * screen.DISPLAY_BYTES_PER_COLOR
    pColor := @byte[screen.screenAddress()][colorOffset]
    'if isDebugLocn(rowIndex, columnIndex)
        'debug("- Screen RC=(", udec_(rowIndex), ", ", udec_(columnIndex), "), ofs=", udec_(colorOffset), ", RGB=(", uhex_(red), uhex_(green), uhex_(blue), ")")
    byte[pColor][0] := color.correctedColor(red)
    byte[pColor][1] := color.correctedColor(green)
    byte[pColor][2] := color.correctedColor(blue)


PRI wait100thSecs(count)
    repeat count
        waitms(10)  ' 1/100 seconds

CON { license }

{{


 -------------------------------------------------------------------------------------------------
  MIT License

  Copyright (c) 2020 Iron Sheep Productions, LLC

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
 =================================================================================================
 
}}

